# --
# Kernel/Modules/PictureUpload.pm
# Modified version of the work:
# Copyright (C) 2010-2025 OFORK, https://o-fork.de
# based on the original work of:
# Copyright (C) 2001-2018 OTRS AG, http://otrs.com/
# --
# $Id: PictureUpload.pm,v 1.1.1.1 2018/07/16 14:49:06 ud Exp $
# --
# This software comes with ABSOLUTELY NO WARRANTY. For details, see
# the enclosed file COPYING for license information (AGPL). If you
# did not receive this file, see http://www.gnu.org/licenses/agpl.txt.
# --

package Kernel::Modules::PictureUpload;

use strict;
use warnings;

our $ObjectManagerDisabled = 1;

sub new {
    my ( $Type, %Param ) = @_;

    my $Self = {%Param};
    bless( $Self, $Type );

    return $Self;
}

sub Run {
    my ( $Self, %Param ) = @_;

    my $LayoutObject = $Kernel::OM->Get('Kernel::Output::HTML::Layout');
    my $Charset      = $LayoutObject->{UserCharset} || 'utf-8';

    my $ParamObject = $Kernel::OM->Get('Kernel::System::Web::Request');

    my $FormID          = $ParamObject->GetParam( Param => 'FormID' );
    my $CKEditorFuncNum = $ParamObject->GetParam( Param => 'CKEditorFuncNum' ) || 0;
    my $ResponseType    = $ParamObject->GetParam( Param => 'responseType' ) // 'json';

    my $ReturnError = sub {
        my (%ErrorParam) = @_;

        if ( $ResponseType eq 'json' ) {
            return $LayoutObject->Attachment(
                ContentType => 'application/json; charset=' . $Charset,
                Content     => $LayoutObject->JSONEncode(
                    Data => {
                        error => {
                            message => $ErrorParam{Message} || '',
                        },
                    },
                ),
                Type    => 'inline',
                NoCache => 1,
            );
        }

        if ( $ErrorParam{BlockName} ) {
            $LayoutObject->Block(
                Name => $ErrorParam{BlockName},
                Data => {
                    CKEditorFuncNum => $CKEditorFuncNum,
                },
            );
        }

        return $LayoutObject->Attachment(
            ContentType => 'text/html; charset=' . $Charset,
            Content     => $LayoutObject->Output( TemplateFile => 'PictureUpload' ),
            Type        => 'inline',
            NoCache     => 1,
        );
    };

    if ( !$FormID ) {
        return $ReturnError->(
            BlockName => 'ErrorNoFormID',
            Message   => 'Need FormID!',
        );
    }

    my $UploadCacheObject = $Kernel::OM->Get('Kernel::System::Web::UploadCache');

    my $ContentID = $ParamObject->GetParam( Param => 'ContentID' );
    if ($ContentID) {

        my @AttachmentData = $UploadCacheObject->FormIDGetAllFilesData(
            FormID => $FormID,
        );

        ATTACHMENT:
        for my $Attachment (@AttachmentData) {
            next ATTACHMENT if !$Attachment->{ContentID};
            next ATTACHMENT if $Attachment->{ContentID} ne $ContentID;

            if (
                $Attachment->{Filename} !~ /\.(png|gif|jpg|jpeg|bmp)$/i
                || substr( $Attachment->{ContentType} || '', 0, 6 ) ne 'image/'
                )
            {
                return $ReturnError->(
                    BlockName => 'ErrorNoImageFile',
                    Message   => 'The file is not an image that can be shown inline!',
                );
            }

            if ( $Attachment->{ContentType} =~ /xml/i ) {

                my %SafetyCheckResult = $Kernel::OM->Get('Kernel::System::HTMLUtils')->Safety(
                    String       => $Attachment->{Content},
                    NoApplet     => 1,
                    NoObject     => 1,
                    NoEmbed      => 1,
                    NoSVG        => 0,
                    NoIntSrcLoad => 0,
                    NoExtSrcLoad => 0,
                    NoJavaScript => 1,
                    Debug        => $Self->{Debug},
                );

                $Attachment->{Content} = $SafetyCheckResult{String};
            }

            return $LayoutObject->Attachment(
                Type => 'inline',
                %{$Attachment},
            );
        }

        return $ReturnError->(
            BlockName => 'ErrorNoFileFound',
            Message   => 'No file found!',
        );
    }

    my %File = $ParamObject->GetUploadAll(
        Param => 'upload',
    );

    if ( !%File ) {
        return $ReturnError->(
            BlockName => 'ErrorNoFileFound',
            Message   => 'No file found!',
        );
    }

    if ( $File{Filename} !~ /\.(png|gif|jpg|jpeg|bmp)$/i || substr( $File{ContentType} || '', 0, 6 ) ne 'image/' ) {
        return $ReturnError->(
            BlockName => 'ErrorNoImageFile',
            Message   => 'The file is not an image that can be shown inline!',
        );
    }

    if ( $File{ContentType} =~ /xml/i ) {

        my %SafetyCheckResult = $Kernel::OM->Get('Kernel::System::HTMLUtils')->Safety(
            String       => $File{Content},
            NoApplet     => 1,
            NoObject     => 1,
            NoEmbed      => 1,
            NoSVG        => 0,
            NoIntSrcLoad => 0,
            NoExtSrcLoad => 0,
            NoJavaScript => 1,
            Debug        => $Self->{Debug},
        );

        $File{Content} = $SafetyCheckResult{String};
    }

    my @AttachmentMeta = $UploadCacheObject->FormIDGetAllFilesMeta(
        FormID => $FormID,
    );

    my $FilenameTmp    = $File{Filename};
    my $SuffixTmp      = 0;
    my $UniqueFilename = '';

    while ( !$UniqueFilename ) {
        $UniqueFilename = $FilenameTmp;

        NEWNAME:
        for my $Attachment ( reverse @AttachmentMeta ) {
            next NEWNAME if $FilenameTmp ne $Attachment->{Filename};

            ++$SuffixTmp;

            if ( $File{Filename} =~ /^(.*)\.(.+?)$/ ) {
                $FilenameTmp = "$1-$SuffixTmp.$2";
            }
            else {
                $FilenameTmp = "$File{Filename}-$SuffixTmp";
            }

            $UniqueFilename = '';
            last NEWNAME;
        }
    }

    $UploadCacheObject->FormIDAddFile(
        FormID      => $FormID,
        Filename    => $FilenameTmp,
        Content     => $File{Content},
        ContentType => $File{ContentType} . '; name="' . $FilenameTmp . '"',
        Disposition => 'inline',
    );

    my $ContentIDNew = '';

    @AttachmentMeta = $UploadCacheObject->FormIDGetAllFilesMeta(
        FormID => $FormID,
    );

    ATTACHMENT:
    for my $Attachment (@AttachmentMeta) {
        next ATTACHMENT if $FilenameTmp ne $Attachment->{Filename};

        $ContentIDNew = $Attachment->{ContentID};
        last ATTACHMENT;
    }

    if ( !$ContentIDNew ) {
        return $ReturnError->(
            BlockName => 'ErrorNoFileFound',
            Message   => 'No file found!',
        );
    }

    my $Session = '';
    if ( $Self->{SessionID} && !$Self->{SessionIDCookie} ) {
        $Session = ';' . $Self->{SessionName} . '=' . $Self->{SessionID};
    }

    my $URL = $LayoutObject->{Baselink}
        . "Action=PictureUpload;FormID=$FormID;ContentID=$ContentIDNew$Session";

    if ( $ResponseType eq 'json' ) {

        return $LayoutObject->Attachment(
            ContentType => 'application/json; charset=' . $Charset,
            Content     => $LayoutObject->JSONEncode(
                Data => {
                    url      => $URL,
                    uploaded => 1,
                    fileName => $FilenameTmp,
                },
            ),
            Type    => 'inline',
            NoCache => 1,
        );
    }

    $LayoutObject->Block(
        Name => 'Success',
        Data => {
            CKEditorFuncNum => $CKEditorFuncNum,
            URL             => $URL,
        },
    );

    return $LayoutObject->Attachment(
        ContentType => 'text/html; charset=' . $Charset,
        Content     => $LayoutObject->Output( TemplateFile => 'PictureUpload' ),
        Type        => 'inline',
        NoCache     => 1,
    );
}

1;